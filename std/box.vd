import std/core;
import std/libc/stdlib;

template <T: type>
struct Box {
	var data: vptr!<T>;

	func constructor(this: &&Box!<T>) -> void {
		this.data = libc::stdlib::malloc(sizeOf!<T>()) as vptr!<T>;
		assert(this.data != vptr::null());
		this.data.value().constructor();
	}

	func constructor(this: &&Box!<T>, value: T) -> void {
		this.data = libc::stdlib::malloc(sizeOf!<T>()) as vptr!<T>;
		assert(this.data != vptr::null());
		this.data.value().constructor(value);
	}

	func destructor(this: &&Box!<T>) -> void {
		this.data.value().destructor();
		libc::stdlib::free(this.data as vptr);
	}

	operator ==(this: Box!<T>, other: Box!<T>) -> bool {
		return this.operator unwrap() == other.operator unwrap();
	}

	operator ==(this: Box!<T>, other: T) -> bool {
		return this.operator unwrap() == other;
	}

	operator unwrap(this: Box!<T>) -> T {
		return this.data.value();
	}

	operator unwrap(this: &Box!<T>) -> &T {
		return &this.data.value();
	}

	operator unwrap(this: &&Box!<T>) -> &&T {
		return &&this.data.value();
	}
}
