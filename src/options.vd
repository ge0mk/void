import std/core;
import std/format;
import std/list;
import std/path;
import std/string;

enum Mode {
	case Compile;
	case Check;
	case Run;
}

enum SymbolType {
	case None;
	case Name;	// only name -> will probably cause duplicate names
	case Full;	// signature is used as name
}

enum OutputType {
	case LLVMAssembly;
	case LLVMBitcode;
}

struct ImportedModule {
	var group: uint;
	var path: String;
	var name: String;

	func constructor(this: &&ImportedModule, group: uint, path: String, name: String) -> void {
		this.group := group;
		this.path := path;
		this.name := name;
	}

	func constructor(this: &&ImportedModule, other: ImportedModule) -> void = default;
	func destructor(this: &&ImportedModule) -> void = default;

	operator =(this: &&ImportedModule, other: ImportedModule) -> void = default;
	operator ==(this: ImportedModule, other: ImportedModule) -> bool = default;
}

struct CompilerOptions {
	var mode: Mode;

	var dump_tokens: bool;
	var dump_ast: bool;
	var dump_program: bool;

	var hide_errors: bool;

	var install_path: String;
	var root_module_path: String;
	var program_name: String;
	var build_dir: String;

	var import_search_paths: List!<Path>;

	var optimization_level: uint; // 0 - 3, 4 = size, 5 = more size

	var target_triple: String;
	var target_data_layout: String;
	var codegen_only: bool;
	var symbol_type: SymbolType;
	var output_type: OutputType;
	var generate_debug_info: bool;
	var codegen_inline_builtin_functions: bool;
	var codegen_asap: bool;
	var codegen_lazy: bool;

	var run: bool;

	var link_libraries: List!<String>;

	var secondary_args: List!<String>;

	func constructor(this: &&CompilerOptions) -> void {
		this.mode := Compile;
		this.symbol_type := Full;
		this.output_type := LLVMAssembly;
	}

	func constructor(this: &&CompilerOptions, other: CompilerOptions) -> void = default;
	func destructor(this: &&CompilerOptions) -> void = default;

	operator =(this: &&CompilerOptions, other: CompilerOptions) -> void = default;
	operator ==(this: CompilerOptions, other: CompilerOptions) -> bool = default;

	func getImportPaths(this: &CompilerOptions, module: &String) -> List!<ImportedModule> {
		var result: List!<ImportedModule>;

		for i in 0u..this.import_search_paths.size() {
			const path = this.import_search_paths[i];

			const file_path = (path / (module + ".vd"));
			const dir_path = (path / module);

			if file_path.isFile() {
				result.append(ImportedModule(i, file_path.toString(), module));
				break;
			} else if dir_path.isDirectory() {
				for file in dir_path.recursiveRange() {
					if file.extension() != "vd" {
						continue;
					}

					const relative_path = Path(List!<String>(file.segments[path.segments.size()..file.segments.size()]));
					var name = "{" + format(i) + "}/" + relative_path.toString();
					name = name.substring(0u..(name.size() - 3));
					result.append(ImportedModule(i, file.toString(), name));
				}
				break;
			}
		}

		return result;
	}

	func getOutputCodePath(this: &CompilerOptions, file_ending: String) -> String {
		return this.build_dir + "/" + this.program_name + file_ending;
	}

	func getOutputBinaryPath(this: &CompilerOptions) -> String {
		return this.build_dir + "/" + this.program_name;
	}

	func getOptimizationLevelAsClangArg(this: &CompilerOptions) -> String {
		match this.optimization_level {
			case 1 -> return "-O1";
			case 2 -> return "-O2";
			case 3 -> return "-O3";
			case 4 -> return "-Os";
			case 5 -> return "-Oz";
			else -> return "-O0";
		}
	}
}
